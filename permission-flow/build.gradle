plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlinx.kover'
    id 'com.vanniktech.maven.publish'
    id 'org.jetbrains.dokka'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        buildConfig false
    }

    testOptions {
        unitTests.all {
            if (name == "testDebugUnitTest") {
                kover {
                    disabled = false
                    binaryReportFile.set(file("$buildDir/coverageReport/coverageReport.bin"))
                }
            }
        }
    }
}

dependencies {
    // Android
    implementation "androidx.appcompat:appcompat:$appCompatVersion"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    // Testing
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    testImplementation "app.cash.turbine:turbine:$turbineVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}

tasks.koverHtmlReport {
    enabled = true
    htmlReportDir.set(layout.buildDirectory.dir("coverageReport/html"))
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}